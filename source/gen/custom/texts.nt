import {
  core.control {grow},
  core.except {from-option},
  core.foreign {UINT32_MAX},
  core.int {max-int, min-int},
  core.list {filter, for-each-N},
  core.random {random-in-range-int},
  core.text {join, pack, unpack},
  core.text.show,
  this.gen {Gen, gen},
  this.gen.custom.runes,
  this.gen.lists {_shrink-list},
}

data config {
| Config(
    min-length: int,
    max-length: ?int,
    rune-generator: ?() -> rune,
    rune-acceptor: (rune) -> bool,
  )
}

inline texts(!c: config): gen(text) {
  let rune-config =
    this.gen.custom.runes.Config of {
      rune-generator = !c::rune-generator,
      rune-acceptor = !c::rune-acceptor,
    }
  in
  let !rune-gen = this.gen.custom.runes.runes(rune-config) in
  Gen of {
    generate = {
      function (size: int) {
        let max-length = from-option(!c::max-length, zext-int32-int(UINT32_MAX)) in
        let text-max-len = trunc-int-int32(min-int(size, max-length)) in
        let text-len = max-int(random-in-range-int(0, text-max-len), !c::min-length) in
        let tmp =
          grow([], text-len, function (acc, _) {
            let r = !rune-gen::generate(size) in
            Cons(core.text.singleton(r), acc)
          })
        in
        join(tmp)
      }
    },
    shrink = {
      function (t: text) {
        pin t = t in
        let rune-seq = unpack(t) in
        pin rune-lists = _shrink-list(!rune-gen)(rune-seq) in
        let result = for-each-N(rune-lists, pack) in
        filter(result, function (t) {
          let len = core.text.length(t) in
          ge-int(len, !c::min-length)
        })
      }
    },
    viewer = core.text.show.as-show,
  }
}
