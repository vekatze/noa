import {
  core.control {grow},
  core.either {from-right},
  core.foreign {UINT32_MAX},
  core.int {max-int, min-int},
  core.list {filter, for-each-N},
  core.random {random-in-range-int},
  core.text {join, pack, unpack},
  core.text.show,
  this.move.gen.custom.rune,
  this.move.gen.list {_shrink-list},
  this.rule.gen {Gen, gen},
}

data config {
| Config(
    min-length: int,
    max-length: ?int,
    rune-generator: ?() -> rune,
    rune-acceptor: (rune) -> bool,
  )
}

inline text-gen(!c: config): gen(text) {
  let Config of {rune-generator, rune-acceptor, max-length, min-length} = !c in
  let rune-config =
    this.move.gen.custom.rune.Config of {
      rune-generator,
      rune-acceptor,
    }
  in
  let !rune-gen = this.move.gen.custom.rune.rune-gen(rune-config) in
  let Gen of {generate} = !rune-gen in
  Gen of {
    generate = {
      function (size: int) {
        let max-length = from-right(max-length, zext-int32-int(UINT32_MAX)) in
        let text-max-len = trunc-int-int32(min-int(size, max-length)) in
        let text-len = max-int(random-in-range-int(0, text-max-len), min-length) in
        let tmp =
          grow([], text-len, function (acc, _) {
            let r = generate(size) in
            Cons(core.text.singleton(r), acc)
          })
        in
        join(tmp)
      }
    },
    shrink = {
      function (t: text) {
        pin t = t in
        let rune-seq = unpack(t) in
        pin rune-lists = _shrink-list(!rune-gen)(rune-seq) in
        let result = for-each-N(rune-lists, pack) in
        filter(result, function (t) {
          let len = core.text.length(t) in
          ge-int(len, min-length)
        })
      }
    },
    viewer = core.text.show.as-show,
  }
}
