import {
  core.show {Show},
  core.text {replace},
  this.gen {Gen, draw, gen},
  this.utils {_print-blue-bold, _print-green-bold, _print-red-bold, _printf-red-bold},
}

data config {
| Config(
    num-of-tests: int,
    max-size: int,
    verbose: bool,
  )
}

define _compute-size(c: config, iteration: int): int {
  let Config of {num-of-tests, max-size} = c in
  let max-size' = sitofp-int-float(max-size) in
  let iteration' = sitofp-int-float(iteration) in
  let num-of-tests' = sitofp-int-float(num-of-tests) in
  let size = mul-float(max-size', div-float(iteration', num-of-tests')) in
  fptosi-float-int(size)
}

data _result(a) {
| _OK
| _Failure(
    iteration: int,
    value: a,
    property: (a) -> bool,
    g: gen(a),
    verbose: bool,
  )
}

define _find-refutation<a>(!property: (a) -> bool, cands: list(a), !g: gen(a), verbose: bool): ?a {
  let Gen of {viewer} = !g in
  let Show of {show = !show} = viewer in
  match cands {
  | Nil =>
    when verbose {
      _print-blue-bold("    ✔ Stop\n")
    };
    none
  | Cons(!cand, rest) =>
    if !property(!cand) {
      when verbose {
        _print-blue-bold("      ✔ ");
        printf("{}\n", [!show(!cand)])
      };
      _find-refutation(!property, rest, !g, verbose)
    } else {
      when verbose {
        _print-red-bold("      ✘ ");
        _printf-red-bold("{}\n", [!show(!cand)])
      };
      Right(!cand)
    }
  }
}

define _repeat-shrink<a>(!cand: a, !property: (a) -> bool, !g: gen(a), verbose: bool): a {
  let Gen of {shrink} = !g in
  let alts = shrink(!cand) in
  match _find-refutation(!property, alts, !g, verbose) {
  | Left(_) =>
    !cand
  | Right(smaller-cand) =>
    _repeat-shrink(smaller-cand, !property, !g, verbose)
  }
}

inline _run-tests<a>(num-of-tests: int, !f: (int) -> _result(a)): either(pair(int, a), unit) {
  let helper =
    define self(iteration: int): either(pair(int, a), unit) {
      if ge-int(iteration, num-of-tests) {
        Right(Unit)
      } else {
        match !f(iteration) {
        | _OK =>
          self(add-int(iteration, 1))
        | _Failure of {iteration, value, property, g, verbose} =>
          when verbose {
            _print-blue-bold("    ↻ Shrink:\n")
          };
          let value' = _repeat-shrink(value, property, g, verbose) in
          Left(Pair(iteration, value'))
        }
      }
    }
  in
  helper(0)
}

inline _check(!c: config): <a>(label: &text, !g: gen(a), property: (a) -> bool) -> unit {
  function <a>(label: &text, !g: gen(a), !property: (a) -> bool): unit {
    let Gen of {viewer} = !g in
    let Show of {show = !show} = viewer in
    let Config of {verbose, num-of-tests} = !c in
    let result =
      when verbose {
        _print-blue-bold("? Check: ");
        printf("{}\n", [*label])
      };
      _run-tests(num-of-tests, function (i) {
        let size = _compute-size(!c, i) in
        let !value = draw(size, !g) in
        if !property(!value) {
          when verbose {
            _print-blue-bold("  ✔ ");
            printf("{}\n", [!show(!value)])
          };
          _OK
        } else {
          when verbose {
            _print-red-bold("  ✘ ");
            _printf-red-bold("{}\n", [!show(!value)])
          };
          _Failure(i, !value, !property, !g, verbose)
        }
      })
    in
    match result {
    | Right(Unit) =>
      _print-green-bold("✔ Pass: ");
      printf("{}\n", [*label])
    | Left(Pair(_, counterexample)) =>
      _print-red-bold("✘ Fail: ");
      printf("{}\n", [*label]);
      _print-red-bold("  → ");
      let txt = !show(counterexample) in
      let txt' on txt = replace(txt, "\n", *"\n    ") in
      let _ = txt in
      _printf-red-bold("{}\n", [txt'])
    }
  }
}

inline _test(label: &text, property: () -> bool): unit {
  if property() {
    _print-green-bold("✔ Pass: ");
    printf("{}\n", [*label])
  } else {
    _print-red-bold("✘ Fail: ");
    printf("{}\n", [*label])
  }
}

data trope {
| Trope(
    check: <a>(name: &text, g: gen(a), property: (a) -> bool) -> unit,
    test: (label: &text, property: () -> bool) -> unit,
  )
}

inline new-suite(!c: config): trope {
  Trope of {
    check = _check(!c),
    test = _test,
  }
}

inline noa: trope {
  new-suite(
    Config of {
      num-of-tests = 100,
      max-size = 100,
      verbose = False,
    },
  )
}

inline noa-verbose: trope {
  new-suite(
    Config of {
      num-of-tests = 100,
      max-size = 100,
      verbose = True,
    },
  )
}
