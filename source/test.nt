import {
  core.list {append, length, reverse},
  core.list.eq,
  core.text {pack, unpack},
  this.move.check {check},
  this.move.gen.float {float-gen},
  this.move.gen.int {int-gen},
  this.move.gen.list {list-gen},
  this.move.gen.one-of {one-of},
  this.move.gen.pair {pair-gen},
  this.move.gen.rune {rune-gen},
  this.move.gen.text {ascii-text-gen, text-gen},
  this.move.make-noa-kit {make-default-noa-kit, make-verbose-noa-kit},
  this.move.test {test},
}

define _find-int(i: int, xs: list(int)): bool {
  match xs {
  | Nil =>
    False
  | Cons(x, xs) =>
    if eq-int(x, i) {
      True
    } else {
      _find-int(i, xs)
    }
  }
}

define _find-float(i: float, xs: list(float)): bool {
  match xs {
  | Nil =>
    False
  | Cons(x, xs) =>
    if gt-float(x, i) {
      True
    } else {
      _find-float(i, xs)
    }
  }
}

define zen(): unit {
  pin k = make-default-noa-kit();
  check(
    k,
    "unpack then pack is identity",
    text-gen,
    function (t) {
      pin t = t;
      pin xs = unpack(t);
      pin t' = pack(xs);
      eq-text(t, t')
    },
  );
  check(
    k,
    "(length as text) == (length as list of chars)",
    text-gen,
    function (t) {
      pin t = t;
      pin xs = unpack(t);
      let len1 = core.text.length(t);
      let len2 = length(xs);
      eq-int(len1, len2)
    },
  );
  check(
    k,
    "reverse(ys) ++ reverse(xs) == reverse(xs ++ ys)",
    pair-gen(list-gen(rune-gen), list-gen(rune-gen)),
    function (p) {
      let Pair(!xs, !ys) = p;
      pin left = append(reverse(!ys), reverse(!xs));
      pin right = reverse(append(!xs, !ys));
      let Eq of {equal} = core.list.eq.as-eq(core.rune.eq.as-eq);
      equal(left, right)
    },
  );
  pin xs = List[1, 2, 3];
  pin x = 4;
  check(
    k,
    "one-of (should fail)",
    one-of(int-gen, xs, x),
    function (x) {
      let b1 = eq-int(x, 1);
      let b2 = eq-int(x, 3);
      let b3 = eq-int(x, 3);
      or(b1, or(b2, b3))
    },
  );
  check(
    k,
    "shrinking texts (should fail)",
    text-gen,
    function (t) {
      let len on t = core.text.length(t);
      let _ = t;
      lt-int(len, 10)
    },
  );
  pin k = make-verbose-noa-kit();
  check(
    k,
    "there is no list that contains 15 (should fail and report [15])",
    list-gen(int-gen),
    function (xs) {
      not(_find-int(15, xs))
    },
  );
  check(
    k,
    "there is no list that contains a value bigger than 15 (should fail and report [15.XXXXXX])",
    list-gen(float-gen),
    function (xs) {
      not(_find-float(15, xs))
    },
  );
  check(
    k,
    "ascii",
    ascii-text-gen,
    function (_) {
      True
    },
  );
  test(
    k,
    "the list `List[1, 2, 3]` contains 2",
    function () {
      pin xs = List[1, 2, 3];
      pin f =
        function (y) {
          eq-int(*y, 2)
        };
      let result = core.list.find(xs, f);
      match result {
      | Left(_) =>
        False
      | Right(_) =>
        True
      }
    },
  )
}

define main(): unit {
  zen()
}
